Message ::= Declaration* Variant+

/* Aliases and selectors */
Declaration ::= Alias? '{' Expression '}' '?'?
Alias ::= Variable '='

/* Pattern and pattern elements */
Variant ::= VariantKey* Pattern
VariantKey ::= Symbol | Literal
Pattern ::= '[' (Text | Placeable)* ']' /* ws: explicit */
Placeable ::= '{' Expression '}'

/* Expressions */
Expression ::= FormatCall | FunctionCall
FormatCall ::= (Literal | Variable) FunctionCall?
FunctionCall ::= Symbol Option*
Option ::= Symbol '=' (Symbol | Literal | Variable)

/* Ignored tokens */
Ignore ::= Comment | WhiteSpace /* ws: definition */

<?TOKENS?>

/* Names */
Variable ::= '$' Symbol /* ws: explicit */
Symbol ::= SymbolStart SymbolChar* /* ws: explicit */
SymbolStart ::= [a-zA-Z] | "_"
               | [#xC0-#xD6] | [#xD8-#xF6] | [#xF8-#x2FF]
               | [#x370-#x37D] | [#x37F-#x1FFF] | [#x200C-#x200D]
               | [#x2070-#x218F] | [#x2C00-#x2FEF] | [#x3001-#xD7FF]
               | [#xF900-#xFDCF] | [#xFDF0-#xFFFD] | [#x10000-#xEFFFF]
SymbolChar ::= SymbolStart | DecimalDigit | "-" | "." | #xB7
             | [#x0300-#x036F] | [#x203F-#x2040]

/* Text */
Text ::= (TextChar | TextEscape)+
TextChar ::= AnyChar - (']' | '{' | Esc)
AnyChar ::= .

/* Literals */
Literal ::= String | Number /* ws: explicit */
String ::= '"' (StringChar | StringEscape)* '"' /* ws: explicit */
Number ::= '-'? DecimalDigit+ ('.' DecimalDigit+)? /* ws: explicit */
StringChar ::= AnyChar - ('"'| Esc)
DecimalDigit ::= [0-9]

/* Escape sequences */
Esc ::= '\'
TextEscape ::= Esc ']' | Esc '{' | UnicodeEscape
StringEscape ::= Esc '"' | UnicodeEscape
UnicodeEscape ::= Esc 'u' HexDigit HexDigit HexDigit HexDigit
                | Esc 'U' HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit
HexDigit ::= [0-9a-fA-F]

/* Comments */
Comment ::= '/*' (AnyChar* - (AnyChar* '*/' AnyChar*)) '*/'

/* WhiteSpace */
WhiteSpace ::= #x9 | #xD | #xA | #x20
