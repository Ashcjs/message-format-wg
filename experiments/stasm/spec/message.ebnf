Message ::= Definition* Variant+

/* Aliases and selectors */
Definition ::= Alias? "{" Expression "}" "?"?
Alias ::= Variable "="

/* Pattern and pattern elements */
Variant ::= VariantKey* Pattern
VariantKey ::= Symbol | Literal
Pattern ::= "[" (Text | Placeable)* "]" /* ws: explicit */
Placeable ::= "{" Expression "}"

/* Expressions */
Expression ::= LiteralFmt | VariableFmt | FunctionCall
LiteralFmt ::= Literal FunctionCall?
VariableFmt ::= Variable FunctionCall?
FunctionCall ::= Symbol Option*
Option ::= Symbol ":" (Symbol | Literal | Variable)

/* Ignored tokens */
Ignore ::= Comment | WhiteSpace /* ws: definition */

<?TOKENS?>

/* Literals & Identifiers*/
Variable ::= "$" Symbol /* ws: explicit */
Symbol ::= (SymbolChar | "_") (SymbolChar | DecimalDigit | "_" | "-")* /* ws: explicit */
Text ::= (TextChar | TextEscape)+
Literal ::= String | Number /* ws: explicit */
String ::= #x22 (StringChar | StringEscape)* #x22 /* ws: explicit */
Number ::= ("-")? DecimalDigit+ ("." DecimalDigit+)? /* ws: explicit */

/* Character classes */
TextChar ::= . - ("]" | "{" | Esc)
StringChar ::= . - (#x22 | Esc)
SymbolChar ::= [a-zA-Z]
DecimalDigit ::= [0-9]
HexDigit ::= [0-9a-fA-F]

/* Escape sequences */
Esc ::= #x5c
TextEscape ::= Esc "]" | Esc "{" | UnicodeEscape
StringEscape ::= Esc #x22 | UnicodeEscape
UnicodeEscape ::= Esc "u" HexDigit HexDigit HexDigit HexDigit
                | Esc "U" HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit

/* Comments */
Comment ::= "/*" (.* - (.* "*/" .*)) "*/"

/* WhiteSpace */
WhiteSpace ::= TAB | VT | FF | SP | NBSP | BOM | USP
TAB ::= #x0009
VT ::= #x000B
FF ::= #x000C
SP ::= #x0020
NBSP ::= #x00A0
BOM ::= #xFEFF
USP ::= [#x0009-#x000D] | #x0020 | #x0085 | #x00A0 | #x1680 | #x180E
      | [#x2000-#x200A] | #x2028 | #x2029 | #x202F | #x205F | #x3000
