Message ::= Definition* Variant+

/* Aliases and selectors */
Definition ::= Alias? "{" Expression "}" "?"?
Alias ::= VariableName "="

/* Pattern and pattern elements */
Variant ::= VariantKey? Pattern
VariantKey ::= (Literal Literal*)
Pattern ::= "[" (Text | Placeable)* "]" /* ws: explicit */
Placeable ::= "{" Expression "}"

/* Expressions */
Expression ::= NumberFmt | VariableFmt | FunctionCall
NumberFmt ::= Number FunctionCall?
VariableFmt ::= VariableName FunctionCall?
FunctionCall ::= Symbol FunctionOpt*
FunctionOpt ::= Symbol ":" (Literal | VariableName)

<?TOKENS?>

/* Literals & Identifiers*/
Text ::= (TextChar | EscapeSeq)+
VariableName ::= "$" Symbol /* ws: explicit */
Literal ::= Symbol | Number /* ws: explicit */
Symbol ::= (SymbolChar | "_") (SymbolChar | DecimalDigit | "_" | "-")* /* ws: explicit */
Number ::= ("-")? DecimalDigit+ ("." DecimalDigit+)? /* ws: explicit */

/* Character classes */
TextChar ::= . - ("]" | "{" | #x5c)
SymbolChar ::= [a-zA-Z]
DecimalDigit ::= [0-9]
HexDigit ::= [0-9a-fA-F]

/* Escape sequences */
EscapeSeq ::= #x5c "]" | #x5c "{" | UnicodeSeq
UnicodeSeq ::= #x5c "u" HexDigit HexDigit HexDigit HexDigit
             | #x5c "U" HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit

/* All whitespace outside text is ignored */
WhiteSpace ::= TAB | VT | FF | SP | NBSP | BOM | USP /* ws: definition */
TAB ::= #x0009
VT ::= #x000B
FF ::= #x000C
SP ::= #x0020
NBSP ::= #x00A0
BOM ::= #xFEFF
USP ::= [#x0009-#x000D] | #x0020 | #x0085 | #x00A0 | #x1680 | #x180E
      | [#x2000-#x200A] | #x2028 | #x2029 | #x202F | #x205F | #x3000
