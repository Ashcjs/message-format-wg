Message ::= TopComment? Alias* Phrase
Alias ::= DocComment? Variable '=' '{' (Expression | Phrase) '}'
Phrase ::= Pattern | Selector+ Variant+

/* Selectors and variants */
Selector ::= '{' Expression '}' '?'
Variant ::= VariantKey+ Pattern
VariantKey ::= String | Nmtoken

/* Pattern and pattern elements */
Pattern ::= '[' (Text | Placeable)* ']' /* ws: explicit */
Placeable ::= '{' Expression '}'

/* Expressions */
Expression ::= Operand Function? | Function
Operand ::= String | Variable
Function ::= Name Option*
Option ::= Name '=' (String | Nmtoken | Variable)

/* Ignored tokens */
Ignore ::= AnyComment | WhiteSpace /* ws: definition */

<?TOKENS?>

/* Text */
Text ::= (TextChar | TextEscape)+
TextChar ::= AnyChar - (']' | '{' | Esc)
AnyChar ::= .

/* Names */
Variable ::= '$' Name /* ws: explicit */
Name ::= NameStart NameChar* /* ws: explicit */
Nmtoken ::= NameChar+ /* ws: explicit */
NameStart ::= [a-zA-Z] | "_"
            | [#xC0-#xD6] | [#xD8-#xF6] | [#xF8-#x2FF]
            | [#x370-#x37D] | [#x37F-#x1FFF] | [#x200C-#x200D]
            | [#x2070-#x218F] | [#x2C00-#x2FEF] | [#x3001-#xD7FF]
            | [#xF900-#xFDCF] | [#xFDF0-#xFFFD] | [#x10000-#xEFFFF]
NameChar ::= NameStart | [0-9] | "-" | "." | #xB7
           | [#x0300-#x036F] | [#x203F-#x2040]

/* Quoted strings */
String ::= '"' (StringChar | StringEscape)* '"' /* ws: explicit */
StringChar ::= AnyChar - ('"'| Esc)

/* Escape sequences */
Esc ::= '\'
TextEscape ::= Esc ']' | Esc '{' | UnicodeEscape
StringEscape ::= Esc '"' | UnicodeEscape
UnicodeEscape ::= Esc 'u' HexDigit HexDigit HexDigit HexDigit
                | Esc 'U' HexDigit HexDigit HexDigit HexDigit HexDigit HexDigit
HexDigit ::= [0-9a-fA-F]

/* Comments */
TopComment ::= '/***' CommentBody '*/'
DocComment ::= '/**' ((AnyChar - '*') CommentBody)? '*/'
AnyComment ::= '/*' ((AnyChar - '*') CommentBody)? '*/'
CommentBody ::= AnyChar* - (AnyChar* '*/' AnyChar*)

/* WhiteSpace */
WhiteSpace ::= #x9 | #xD | #xA | #x20
